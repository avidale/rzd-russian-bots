root:
    [$Route? $From $To ($When|на? $Near $Ticket?)?]
    [$Route? ($WeakFrom $WeakTo) ($When|на? $Near $Ticket?)?]
    [$Route $From? $To ($When|на? $Near $Ticket?)?]
    [$Route $From $To? ($When|на? $Near $Ticket?)?]
    [$Route? $From? $To? ($When|на? $Near $Ticket?)]

slots:
    from:
        source: $WeakFrom
        type: YANDEX.GEO
    to:
        source: $WeakTo
        type: YANDEX.GEO
    when:
        source: $WeakWhen
        type: YANDEX.DATETIME
    near:
        source: $Near

filler:
    алиса
    $YANDEX.NONSENSE

$Near:
    %lemma
    самый близкий | ближайший | следующий
$From:
    %lemma
    $FromPrep? $WeakFrom
$FromPrep:
    от
    из
    с
$To:
    %lemma
    $ToPrep? $WeakTo
$ToPrep:
    до
    в
    на
    к
$When:
    %lemma
    $WhenPrep? $WeakWhen
$WhenPrep:
    на
    в
$WeakFrom:
    $Poi
$WeakTo:
    $Poi
$WeakWhen:
    $Date
$Poi:
    %lemma
    $YANDEX.GEO
$Date:
    %lemma
    $YANDEX.DATETIME
$Route:
    ($Find|$Ticket)+
$Find:
    %lemma
    найди | покажи
$Ticket:
    %lemma
    поезд | билет
